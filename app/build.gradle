apply plugin: 'com.android.application'

static def getProperty(String filename, String property) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(filename))
    return properties.getProperty(property)
}

static def quoted(String value){
    return "\"${value}\""
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "edu.cnm.deepdive.deepdivegallery12presentation"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "CLIENT_ID", quoted(
                getProperty("$projectDir/../../../services/gallery12-presentation.properties", "client_id"))
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    buildFeatures {
        viewBinding true
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Basic Android widgets
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Material Design components
    implementation 'com.google.android.material:material:1.3.0'

    // Fragment navigation dependencies.
    def navigation_version = '2.3.3'
    implementation "androidx.navigation:navigation-fragment:$navigation_version"
    implementation "androidx.navigation:navigation-ui:$navigation_version"

    // Google Sign In dependency
    implementation 'com.google.android.gms:play-services-auth:19.0.0'

    // OkHttp logging dependency
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}